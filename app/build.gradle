/*
 * Copyright (c) 2018 stanwood Gmbh
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'project-report'
apply plugin: 'kotlin-android-extensions'


Properties properties = new Properties()
def propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    properties.load(propertiesFile.newDataInputStream())
}
def bitriseApiToken = properties.getProperty('bitrise.api.token')

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.stanwood.bitrise"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        buildConfigField "String", "BITRISE_API_BASE_URL", "\"https://api.bitrise.io/\""
        buildConfigField "int", "DEFAULT_PAGE_SIZE", "10"
        buildConfigField "int", "PAGE_LOAD_THRESHOLD", "2"
        buildConfigField "String", "BITRISE_API_TOKEN", properties.containsKey('bitrise.api.token')? "\"$bitriseApiToken\"" : "null"
        buildConfigField "String", "API_DATE_TIME_FORMAT", "\"yyyy-MM-dd'T'HH:mm:ss'Z'\""
        buildConfigField "long", "DOWNLOAD_STATUS_REFRESH_DELAY", "500L"
        buildConfigField "String", "REPO_URL", "\"https://github.com/stanwood/Bitrise_Android\""
        manifestPlaceholders.appName = "@string/app_name"
    }
    androidExtensions {
        experimental = true
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        final buildTypeName = variant.buildType.name
        final flavorName = variant.flavorName
        switch (buildTypeName) {
        // release is handled in defaultConfig, but could also be handled here
            case 'debug':
                variant.mergedFlavor.manifestPlaceholders.appName = "$buildNumber Bitrise Unofficial $flavorName $buildTypeName"
                break
        }
    }
    packagingOptions {
        exclude "META-INF/**"
    }
}

dependencies {
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.4.0"
    implementation "com.squareup.retrofit2:converter-gson:2.4.0"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"
    implementation "com.squareup.okhttp3:logging-interceptor:3.10.0"

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.0-alpha'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.0-alpha'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Glide
    implementation "com.github.bumptech.glide:glide:4.8.0"
    annotationProcessor "com.github.bumptech.glide:compiler:4.8.0"

    // UI
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"

    // AndroidX
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.preference:preference:1.1.0-alpha01"
    implementation "com.google.android.material:material:1.0.0"

    // Arch
    implementation "android.arch.navigation:navigation-fragment:1.0.0-alpha07"

    // Other
    implementation "com.google.android.gms:play-services-oss-licenses:16.0.1"
    implementation "com.jakewharton.timber:timber:4.7.0"
    implementation "org.ocpsoft.prettytime:prettytime:4.0.1.Final"
    implementation "joda-time:joda-time:2.9.9"
    implementation "org.koin:koin-android:1.0.1"
}
